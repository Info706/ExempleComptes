plugins {
    id 'eclipse'
    id 'idea'
}

repositories {
    mavenCentral()
}

// configuration encodage pour eclipse
def eclipseResourceEncoding = tasks.register('eclipseResourceEncoding')

tasks.named('eclipse'){
	dependsOn(eclipseResourceEncoding)
}

eclipseResourceEncoding.configure {	
	ext.outputFile = file('.settings/org.eclipse.core.resources.prefs')
	outputs.file(outputFile).withPropertyName('outputFile')
	
	doLast {
		Properties eclipseEncodingProperties = new Properties(Collections.singletonMap('eclipse.preferences.version','1'))
		if (outputFile.exists()) {
			outputFile.withInputStream { eclipseEncodingProperties.load(it) }
		}
		eclipseEncodingProperties.put('encoding/<project>', 'UTF-8')
		outputFile.withOutputStream { eclipseEncodingProperties.store(it, 'generated by '+name) }
		eclipseEncodingProperties.list(System.out)
	}
}

// for automatic project synchronization (eclipse)
eclipse.synchronizationTasks(eclipseResourceEncoding)

tasks.eclipse.doLast {
    File f = file('.settings/org.eclipse.core.resources.prefs')
    f.write('eclipse.preferences.version=1\n')
    f.append('encoding/<project>=UTF-8')
}

// configuration encodage pour IntelliJ
idea {
  project {
    ipr {
      withXml { xmlProvider ->
        def encoding = xmlProvider.asNode().component.find { it.@name == "Encoding" }
        encoding.appendNode("file", [url: "PROJECT", charset: "UTF-8"])
      }
    }
  }
}